buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.1'
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:1.0.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
    maven { url "http://mente.github.io/facebook-api-android-aar" }
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }
    buildTypes {
        release {
            runProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }
    productFlavors {
        defaultFlavor {
            proguardFile 'proguard-rules.txt'
        }
    }

    sourceSets {
        instrumentTest.setRoot('src/test')
    }

    packagingOptions {
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }
}

apply plugin: 'android-unit-test'

ext.googlePlayServicesVersion = '4.2.42'
ext.androidAnnotationsVersion = '3.0.1';
ext.springVersion = '1.0.1.RELEASE';
ext.mockitoVersion = '1.9.5';
ext.picassoVersion = '2.2.0';

configurations {
    apt
}

dependencies {
    // google services
    compile "com.google.android.gms:play-services:${googlePlayServicesVersion}"
    compile 'com.android.support:support-v4:19.0.+'

    // gson
    compile 'com.google.code.gson:gson:2.2.4'

    // spring
    compile "org.springframework.android:spring-android-rest-template:${springVersion}"

    // android annotations
    apt "org.androidannotations:androidannotations:${androidAnnotationsVersion}"
    compile "org.androidannotations:androidannotations-api:${androidAnnotationsVersion}"

    // utils
    compile 'org.apache.commons:commons-lang3:3.1'

    // picasso
    compile "com.squareup.picasso:picasso:${picassoVersion}"

    // parse
    compile files('libs/Parse-1.4.0/Parse-1.4.0.jar')

    // facebook
    compile 'com.facebook:facebook-android-sdk:+@aar'

    // circle image view
    compile 'de.hdodenhof:circleimageview:1.0.1'

    // testing
    instrumentTestCompile 'org.robolectric:robolectric:2.2'
    instrumentTestCompile 'junit:junit:4.11'
    instrumentTestCompile 'com.squareup:fest-android:1.0.+'
    instrumentTestCompile "org.mockito:mockito-all:${mockitoVersion}"
    testCompile 'org.robolectric:robolectric:2.2'
    testCompile 'junit:junit:4.11'
    testCompile 'com.squareup:fest-android:1.0.+'
    testCompile "org.mockito:mockito-all:${mockitoVersion}"
}

/* annotations stuff */

def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}

android.applicationVariants.all { variant ->
    def aptOutputDir = project.file("build/source/apt")
    def aptOutput = new File(aptOutputDir, variant.dirName)
    println "****************************"
    println "variant: ${variant.name}"
    println "manifest:  ${variant.processResources.manifestFile}"
    println "aptOutput:  ${aptOutput}"
    println "****************************"

    android.sourceSets[getSourceSetName(variant)].java.srcDirs += aptOutput.getPath()

    variant.javaCompile.options.compilerArgs += [
            '-processorpath', configurations.apt.getAsPath(),
            '-processor', 'org.androidannotations.AndroidAnnotationProcessor',
            '-AandroidManifestFile=' + variant.processResources.manifestFile,
            '-s', aptOutput
    ]

    variant.javaCompile.source = variant.javaCompile.source.filter { p ->
        return !p.getPath().startsWith(aptOutputDir.getPath())
    }

    variant.javaCompile.doFirst {
        aptOutput.mkdirs()
    }
}
/* end of annotations stuff */
